#!/usr/bin/gnuplot
set print "-"
print "Content-Type: image/svg+xml"
print ""

#Data format:
#18-Aug-19 14:00:03, 21.5, 92.0
#18-Aug-19 14:15:04, 22.6, 90.8
#18-Aug-19 14:30:03, 22.5, 90.0

set xdata time
set timefmt "%d-%b-%y %H:%M:%S"
set datafile separator ","

set style line 20 lc rgb "0x808080" lt 1 lw 1
set style line 21 lc rgb "0xd0d0d0" lt 1 lw 1
set grid xtics ytics mxtics mytics ls 20, ls 21
set mxtics 4
set mytics 5

set yrange [0:100]
set y2range [-20:30]
set y2tics nomirror

set key left bottom

set style line 10 lt rgb "0xc0c0ff" linewidth 2
set style line 11 lt rgb "0x800000" linewidth 2
set style line 12 lt rgb "0x5050ff" linewidth 2

set terminal svg size 1024,768 name "Shed_temp"
# set output 'templog.png'

plot '/ramdisk/1week.txt' using 1:3 with lines ls 10 title "RH", \
 '/ramdisk/1week.txt' using 1:2 with lines axis x1y2 ls 11 title "Temp", \
 '/ramdisk/1week.txt' using 1:($2-(100-$3)/5) with lines axis x1y2 ls 12 title "DP"
